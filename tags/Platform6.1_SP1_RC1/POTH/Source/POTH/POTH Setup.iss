;
; Script originally generated by the ASCOM Driver Installer Script Generator 6.0.0.0
; Generated and modified by Jon Brewster on 5/27/2011
;

#define AppName "ASCOM POTH (Plain Old Telescope Handset) Hub"
#define AppNameASCOM "POTH Hub"
#define AppNameMedium "POTH"
#define AppNameShort "POTH"
#define AppSource "Source\POTH"
#define AppVersion "6.0.3"

[Setup]
AppName={#AppName}
AppVersion={#AppVersion}
AppVerName={#AppName} {#AppVersion}
AppPublisher=Jon Brewster <jon@brewsters.net>
AppPublisherURL=mailto:jon@brewsters.net
AppSupportURL=http://tech.groups.yahoo.com/group/ASCOM-Talk/
AppUpdatesURL=http://ascom-standards.org/
VersionInfoVersion=1.0.0
MinVersion=0,5.0.2195sp4
DefaultDirName={cf}\ASCOM\Telescope
DisableDirPage=yes
DisableProgramGroupPage=yes
OutputDir=.
OutputBaseFilename={#AppNameMedium} ({#AppVersion}) Setup
Compression=lzma/Max
SolidCompression=true
; Put there by Platform if Driver Installer Support selected
WizardImageFile=C:\Program Files\ASCOM\Platform 6 Developer Components\Installer Generator\Resources\WizardImage.bmp
LicenseFile=C:\Program Files\ASCOM\Platform 6 Developer Components\Installer Generator\Resources\CreativeCommons.txt
; {cf}\ASCOM\Uninstall\Telescope folder created by Platform, always
UninstallFilesDir={cf}\ASCOM\Uninstall\Telescope\{#AppNameMedium}

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Dirs]
Name: {cf}\ASCOM\Uninstall\Telescope\{#AppNameMedium}
; TODO: Add subfolders below {app} as needed (e.g. Name: "{app}\MyFolder")

[Icons]
Name: {userdesktop}\POTH; Filename: {app}\{#AppNameShort}.exe; Tasks: DesktopIcons
Name: {userdesktop}\Uninstall POTH; Filename: {uninstallexe}; Tasks: DesktopIcons

;  Add an option to install the source files
[Tasks]
Name: SourceCode; Description: Install the source files; Flags: unchecked
Name: DesktopIcons; Description: Install the desktop icons; Flags: unchecked

[Files]
Source: {#AppNameShort}.exe; DestDir: {app}; Flags: ignoreversion; AfterInstall: RegASCOM()
; Require a read-me HTML to appear after installation, maybe driver's Help doc
Source: POTHHelp.htm; DestDir: {app}; Flags: ignoreversion isreadme
; Optional source files (COM and .NET aware)
; Source: "*"; Excludes: *.zip,*.exe,*.dll, \bin\*, \obj\*; DestDir: "{app}\{#AppSource}"; Tasks: SourceCode; Flags: recursesubdirs
Source: astro32.bas; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: astro32.dll; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: AxisRates.cls; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: Bug72T-sm.bmp; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: Dome.cls; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: DomeHW.bas; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: Domecontrol.cls; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: ErrorConstants.bas; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: Focuser.cls; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: FocuserHW.bas; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: frmHandBox.frm; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: frmHandBox.frx; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: frmSetup.frm; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: frmSetup.frx; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: frmShow.frm; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: frmShow.frx; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: hotlink.cur; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: IObjectSafety.idl; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: IObjectSafety.tlb; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: ObjectSafety.bas; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: POTH Setup.iss; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: POTH.vbp; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: POTH.vbw; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: POTHHelp.htm; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: POTH-ref.exe; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: Public.bas; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: Rate.cls; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: ReadMe.txt; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: register.bat; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: saturn.ico; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: saturnc.jpg; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: ScopeHW.bas; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: Startup.bas; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: Telescope.cls; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: TrackingRates.cls; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: unregister.bat; DestDir: {app}\{#AppSource}; Tasks: SourceCode
Source: WindowsAPI.bas; DestDir: {app}\{#AppSource}; Tasks: SourceCode
; TODO: Add other files needed by your driver here (add subfolders above)

;Only if COM Local Server
[Run]
Filename: {app}\{#AppNameShort}.exe; Parameters: /regserver
; old ASCOM register code used prior to this script doing it directly
; Filename: "{app}\{#AppNameShort}.exe"; Parameters: "-r"

;Only if COM Local Server
[UninstallRun]
; old ASCOM unregister code used prior to this script doing it directly
; Filename: "{app}\{#AppNameShort} Driver.exe"; Parameters: "-ur"
Filename: {app}\{#AppNameShort}.exe; Parameters: /unregserver

;  DCOM setup for COM local Server, needed for TheSky
[Registry]
#define AppClsid "{{0A21726F-E58B-4461-85A9-9AA739E6E42F}"
; Telescope CLSID (mind the leading/extra '{')
#define AppClsid1 "{{6E429B65-53BF-4379-B6D7-F2334BCD8707}"
; Dome CLSID (mind the leading/extra '{')
#define AppClsid2 "{{83D12F01-81DB-401E-97AF-E0D5045F73C0}"
; Focuser CLSID (mind the leading/extra '{')
#define AppClsid3 "{{3E0E8170-779A-4FA6-8D1D-DE2D0D49419B}"

; set the DCOM access control for TheSky on the Telescope interface
Root: HKCR; Subkey: CLSID\{#AppClsid1}; ValueType: string; ValueName: AppID; ValueData: {#AppClsid1}
Root: HKCR; Subkey: AppId\{#AppClsid1}; ValueType: string; ValueData: {#AppName}
Root: HKCR; Subkey: AppId\{#AppClsid1}; ValueType: string; ValueName: AppID; ValueData: {#AppClsid1}
Root: HKCR; Subkey: AppId\{#AppClsid1}; ValueType: dword; ValueName: AuthenticationLevel; ValueData: 1
; set the DCOM access control for TheSky on the Dome interface
Root: HKCR; Subkey: CLSID\{#AppClsid2}; ValueType: string; ValueName: AppID; ValueData: {#AppClsid2}
Root: HKCR; Subkey: AppId\{#AppClsid2}; ValueType: string; ValueData: {#AppName}
Root: HKCR; Subkey: AppId\{#AppClsid2}; ValueType: string; ValueName: AppID; ValueData: {#AppClsid2}
Root: HKCR; Subkey: AppId\{#AppClsid2}; ValueType: dword; ValueName: AuthenticationLevel; ValueData: 1
; set the DCOM access control for TheSky on the Focuser interface
Root: HKCR; Subkey: CLSID\{#AppClsid3}; ValueType: string; ValueName: AppID; ValueData: {#AppClsid3}
Root: HKCR; Subkey: AppId\{#AppClsid3}; ValueType: string; ValueData: {#AppName}
Root: HKCR; Subkey: AppId\{#AppClsid3}; ValueType: string; ValueName: AppID; ValueData: {#AppClsid3}
Root: HKCR; Subkey: AppId\{#AppClsid3}; ValueType: dword; ValueName: AuthenticationLevel; ValueData: 1
; set the DCOM key for the executable as a whole
Root: HKCR; Subkey: AppId\{#AppNameShort}.exe; ValueType: string; ValueName: AppID; ValueData: {#AppClsid}
; CAUTION! DO NOT EDIT - DELETING ENTIRE APPID TREE WILL BREAK WINDOWS!
Root: HKCR; Subkey: AppId\{#AppClsid1}; Flags: uninsdeletekey
Root: HKCR; Subkey: AppId\{#AppClsid2}; Flags: uninsdeletekey
Root: HKCR; Subkey: AppId\{#AppClsid3}; Flags: uninsdeletekey
Root: HKCR; Subkey: AppId\{#AppNameShort}.exe; Flags: uninsdeletekey

[Code]
//
// Before the installer UI appears, verify that the (prerequisite)
// ASCOM Platform 6.0 or greater is installed, including both Helper
// components. Utility is required for all types (COM and .NET)!
//
function InitializeSetup(): Boolean;
var
   U : Variant;
   H : Variant;
begin
   Result := FALSE;  // Assume failure
   // check that the DriverHelper and Utilities objects exist, report errors if they don't
   try
      H := CreateOLEObject('DriverHelper.Util');
   except
      MsgBox('The ASCOM DriverHelper object has failed to load, this indicates a serious problem with the ASCOM installation', mbInformation, MB_OK);
   end;
   try
      U := CreateOLEObject('ASCOM.Utilities.Util');
   except
      MsgBox('The ASCOM Utilities object has failed to load, this indicates that the ASCOM Platform has not been installed correctly', mbInformation, MB_OK);
   end;
   try
      if (U.IsMinimumRequiredVersion(6,0)) then	// this will work in all locales
         Result := TRUE;
   except
   end;
   if(not Result) then
      MsgBox('The ASCOM Platform 6.0 or greater is required for this driver.', mbInformation, MB_OK);
end;

//
// Register and unregister the driver with the Chooser
// We already know that the Helper is available
//
procedure RegASCOM();
var
   P: Variant;
begin
   P := CreateOleObject('ASCOM.Utilities.Profile');
   P.DeviceType := 'Telescope';
   P.Register('{#AppNameShort}.Telescope', '{#AppNameASCOM}');
   P.DeviceType := 'Dome';
   P.Register('{#AppNameShort}.Dome', '{#AppNameASCOM}');
   P.DeviceType := 'Focuser';
   P.Register('{#AppNameShort}.Focuser', '{#AppNameASCOM}');
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
   P: Variant;
begin
   if CurUninstallStep = usUninstall then
   begin
     P := CreateOleObject('ASCOM.Utilities.Profile');
     P.DeviceType := 'Telescope';
     P.Unregister('{#AppNameShort}.Telescope');
     P.DeviceType := 'Dome';
     P.Unregister('{#AppNameShort}.Dome');
     P.DeviceType := 'Focuser';
     P.Unregister('{#AppNameShort}.Focuser');
  end;
end;
