;
; Script generated by the ASCOM Driver Installer Script Generator 1.3.0.0
; Generated by Jordan Schaenzle on 11/11/2010 (UTC)
;
[Setup]
AppId=OptecPyxisPrograms
;Do not include verison number in AppName. Displayed thoroughout the install process
AppName=Optec Pyxis Rotator Programs    
AppVerName=Optec Pyxis Rotator Programs 2.0.0
AppVersion=2.0.0
AppPublisher=Jordan Schaenzle <support@optecinc.com>
AppPublisherURL=mailto:support@optecinc.com
AppSupportURL=http://tech.groups.yahoo.com/group/ASCOM-Talk/
AppUpdatesURL=http://ascom-standards.org/
ArchitecturesInstallIn64BitMode=x64
VersionInfoVersion=2.0.0
MinVersion=0,5.0.2195sp4
DefaultDirName="{pf}\Optec\Pyxis"
DisableDirPage=yes
DisableProgramGroupPage=yes
OutputDir="."
OutputBaseFilename="OptecPyxis_Installer(2.0.0)"
Compression=lzma
SolidCompression=yes
; Put there by Platform if Driver Installer Support selected
WizardImageFile="C:\Program Files (x86)\ASCOM\InstallGen\Resources\WizardImage.bmp"
LicenseFile="C:\Program Files (x86)\ASCOM\InstallGen\Resources\CreativeCommons.txt"
UninstallFilesDir="{app}\Uninstall"
UninstallDisplayIcon="I:\Software Development\ICONS\Pyxis LE\Rotator.ico"
SetupLogging=yes



[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

; THIS SECTION LISTS THE DIRECTORIES TO BE INSATLLED/UNINSTALLED
[Dirs]
; Create a folder for the Uninstall Log
Name: "{pf}\Optec\Pyxis\Uninstall"
; Create a directory for the file necessary to the program/driver
Name: "{pf}\Optec\Pyxis\Executable Files"
; Create a directory for the help files and documentation
Name: "{pf}\Optec\Pyxis\Documentation"


; THIS SECTION DEFINES THE COMPONENTS THAT USERS CAN CHOOSE TO INSTALL
[Components]
Name: "ASCOM"; Description: "ASCOM Driver for Optec Pyxis"; Types: full;
Name: "Control"; Description:"Control Program (Non-ASCOM) for Optec Pyxis"; Types:full;
Name: "Control/desktopIcon"; Description:"Place Icon on my Desktop"; Types:full;


; THIS SECTION LISTS THE FILES TO BE INSTALLED/UNSTALLED 
[Files] 
; Install the main ASCOM driver dll
Source: "D:\ASCOM REPO\Drivers and Simulators\Optec Drivers\Pyxis\Pyxis\bin\Release\ASCOM.Pyxis.Rotator.dll"; DestDir: "{app}\Executable Files";  Components:"ASCOM";
; Install the main control program 
Source: "D:\ASCOM REPO\Drivers and Simulators\Optec Drivers\Pyxis\Pyxis Rotator Control\bin\Release\Pyxis Rotator Control.exe"; DestDir: "{app}\Executable Files"; Components:"Control";
; Install the EventLogger dll to be used by all
Source: "D:\ASCOM REPO\Drivers and Simulators\Optec Drivers\Pyxis\Dependencies\OptecEventLogger\bin\Release\Optec.EventLogger.dll"; DestDir: "{app}\Executable Files"; Components:Control or ASCOM
; Install ReadMe file with 
Source: "D:\ASCOM REPO\Drivers and Simulators\Optec Drivers\Pyxis\Installer Files\RevisionHistory.txt"; DestDir: "{app}\Documentation"; Flags: isreadme
; Install the Documentation file (.chm)
Source: "I:\Software Development\HelpNDoc\Pyxis\PyxisHelp.chm"; DestDir: "{app}\Documentation"; Flags: isreadme;
; Install the Icon for the device
Source: "I:\Software Development\ICONS\Pyxis LE\Rotator.ico"; DestDir: "{app}\Executable Files"; Components: Control;


;THIS SECTION LISTS THE ICONS THAT ARE TO BE CREATED ON THE TARGET MACHINE
[Icons]
Name: "{commondesktop}\Optec Pyxis Rotator Control";  Filename:"{app}\Executable Files\Pyxis Rotator Control.exe"; Components: Control/desktopIcon; IconFilename:"{app}\Executable Files\Rotator.ico"
Name: "{commonprograms}\Optec\Pyxis\Pyxis Rotator Control";  Filename:"{app}\Executable Files\Pyxis Rotator Control.exe"; Components: Control; IconFilename:"{app}\Executable Files\Rotator.ico"


; Only if driver is .NET
[Run]
; Only for .NET assembly/in-proc drivers
Filename: "{dotnet20}\regasm.exe";    Parameters: "/codebase ""{app}\Executable Files\ASCOM.Pyxis.Rotator.dll"""; Flags: runhidden; Components:"ASCOM"; StatusMsg: Registering Driver for COM;
Filename: "{dotnet2032}\regasm.exe";  Parameters: "/codebase ""{app}\Executable Files\ASCOM.Pyxis.Rotator.dll"""; Flags: runhidden; Components:"ASCOM"; Check: IsWin64; StatusMsg: Registering Driver for 32 bit COM;

; Only if driver is .NET
[UninstallRun]
; Only for .NET assembly/in-proc drivers
Filename: "{dotnet20}\regasm.exe";    Parameters: "-u ""{app}\Executable Files\ASCOM.Pyxis.Rotator.dll"""; Flags: runhidden; Components:"ASCOM"; StatusMsg: Registering Driver for COM;
Filename: "{dotnet2032}\regasm.exe";  Parameters: "-u ""{app}\Executable Files\ASCOM.Pyxis.Rotator.dll"""; Flags: runhidden; Components:"ASCOM"; Check: IsWin64; StatusMsg: Registering Driver for 32 bit COM;

; THIS SECTION LISTS ANY ADDITION FILES TO DELETE THAT THE PROGRAM MAY HAVE CREATED
[UninstallDelete]
Type: files; Name:"{userappdata}\Optec\Pyxis\PyxisSettings.xml";
Type: dirifempty; Name:"{userappdata}\Optec\Pyxis";
Type: dirifempty; Name:"{userappdata}\Optec";

; THIS SECTION IS USED TO CREATE CUSTOM PASCEL FUNCTIONS TO BE RUN DURING INSTALL
[CODE]
//
// Before the installer UI appears, verify that the minimum (prerequisite)
// ASCOM Platform is installed.
//

function IsDotNetDetected(version: string; service: cardinal): Boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1.4322'     .NET Framework 1.1
//    'v2.0.50727'    .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key: string;
    install, serviceCount: cardinal;
    success: boolean;
begin
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + version;
    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;
    // .NET 4.0 uses value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;
    result := success and (install = 1) and (serviceCount >= service);
end;


function InitializeSetup(): Boolean;
var
   H : Variant;
begin
   Result := FALSE;  // Assume failure

   try               // Will catch all errors including missing reg data
      H := CreateOLEObject('ASCOM.Utilities.Util');  // Assure both are available
      if (Not H.IsMinimumRequiredVersion(5,5)) then   // Check for ASCOM Platform 5.5 at least
	  begin
		MsgBox('The ASCOM Platform 5.5 or greater is required for this driver.', mbInformation, MB_OK);
        Result := FALSE;
	  end
	  else if (Not IsDotNetDetected('v3.5',0)) then     // Check for .NET Platform 
	  begin
         MsgBox('The Microsoft .NET Framework 3.5 is required for this driver. Please download it before continuing.', mbInformation, MB_OK);
	     Result := FALSE;
	  end
	  else Result := True;
   except
   end;      
end;



