;
; Script generated by the ASCOM Driver Installer Script Generator 1.2.0.0
; Generated by Peter Simpson on 09/08/2008 (UTC)
; Changed to Platform updater by Bob Denny 17-Nov-08
;
; Internationalised Platform tests in [Code] section - Peter Simpson - 29/12/2009

[Setup]
AppName=ASCOM Camera Simulator Updater
AppVerName=ASCOM Camera Simulator Updater 5.0.9
AppVersion=5.0.9
AppPublisher=ASCOM initiative
AppPublisherURL=http://ascom-standards.org/
AppSupportURL=http://tech.groups.yahoo.com/group/ASCOM-Talk/
AppUpdatesURL=http://ascom-standards.org/
MinVersion=0,5.0.2195sp4
DefaultDirName="{cf}\ASCOM\Camera"
DisableDirPage=yes
DisableProgramGroupPage=yes
OutputDir="."
OutputBaseFilename="CameraSimulatorUpdate(5.0.9a)"
Compression=lzma
SolidCompression=yes
; Put there by Platform if Driver Installer Support selected
WizardImageFile="C:\Program Files\ASCOM\InstallGen\Resources\WizardImage.bmp"
; {cf}\ASCOM\Uninstall\Camera folder created by Platform, always
Uninstallable=no
DirExistsWarning=no
UninstallFilesDir="{cf}\ASCOM\Uninstall\Camera\CCDSimulator"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Dirs]
Name: "{cf}\ASCOM\Uninstall\Camera\CCDSimulator"

;  Add an option to install the source files
[Tasks]
Name: source; Description: Install the Source files; Flags: unchecked

[Files]
Source: "unuran-0.6.0.dll"; DestDir: "{win}";
Source: "CCDSimulator.dll"; DestDir: "{cf}\ASCOM\Camera";
Source: "UNURANReadMe.txt"; DestDir: "{cf}\ASCOM\Camera";
Source: "*"; Excludes: *.exe; DestDir: "{cf}\ASCOM\Camera\Source\Camera Simulator"; Tasks: source; Flags: recursesubdirs

;
; Next 2 sections are to remove traces of stand-alone driver installation of this
; Platform component that may have been made by installing the 5.0.8 simulator
;
[Registry]
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\Uninstall\ASCOM CCDSimulator Camera Driver_is1"; ValueType: none; Flags: deletekey

[Run]
Filename: "{cmd}"; Parameters: "/c del /q /f ""{cf}\ASCOM\Uninstall\Camera\CCDSimulator\*.*"""; Flags: runhidden
Filename: "{cmd}"; Parameters: "/c rmdir ""{cf}\ASCOM\Uninstall\Camera\CCDSimulator"""; Flags: runhidden

[CODE]
//
// Before the installer UI appears, verify that the (prerequisite)
// ASCOM Platform 5.x is installed, including both Helper components.
// Helper is required for all types (COM and .NET)!
//
function InitializeSetup(): Boolean;
var
   H : Variant;
   H2 : Variant;
   PlatVerString : String;
   PlatVer : Variant;
   DoubleValue : Variant;
   DoubleValueString : String;
   Separator : String;
begin
   Result := FALSE;  // Assume failure
   try               // Will catch all errors including missing reg data
      H := CreateOLEObject('DriverHelper.Util');  // Assure both are available
      H2 := CreateOleObject('DriverHelper2.Util');

      DoubleValue := 1.0 / 3.0; // Create a real number of value 0.33333
      DoubleValueString := DoubleValue; // Get the real number as a string including this system's decimal separator
      Separator := Copy(DoubleValueString,2,1); // Parse out the decimal separator

      PlatVerString := H2.PlatformVersion; // Get the Platform version string (which has "." as its decimal separator)
      StringChangeEx(PlatVerString, '.', Separator, True); // Change the "." to the current system's decimal separator
      // MsgBox('DoubleValue: ' + Separator + ' ' + platVerString, mbCriticalError, MB_OK)

      PlatVer := PlatVerString; // Create a variant from the modified string containing this system's decimal separator
      if (PlatVer < 5.0) then // Check for installed platform below 5
          begin
            MsgBox('ASCOM Platform 5 or later is required for this simulator to work correctly. You must update from Platform 4 to Platform 5 before continuing, you can download the update from http:\\www.ascom-standards.org', mbCriticalError, MB_OK)
          end
      else if (PlatVer >= 6.0) then // Check for installed platform of 6.0 or above
              MsgBox('This update is only validated for use on Platform 5.x, you already have the latest simulator in your platform install.', mbCriticalError, MB_OK)
           else // Platform 5.x is installed so allow the installation to proceed
              Result:= TRUE;
   except
      begin
        MsgBox('ASCOM Platform 5 or later is required for this simulator to work correctly. You must install the ASCOM Platform 5.0b before continuing, you can download the update from http:\\www.ascom-standards.org', mbCriticalError, MB_OK);
      end;
   end;

end;
